buildscript {
    ext.kotlin_version = '1.2.70'

    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url 'https://oss.jfrog.org/oss-snapshot-local/'
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "me.champeau.gradle:antlr4-gradle-plugin:0.1.1-SNAPSHOT"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'me.champeau.gradle.antlr4'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

antlr4 {
    source = file('src/main/antlr')
    output = file('src/generated/java/edu/kspt/cfgbuilder')
    extraArgs = ['-package', 'edu.kspt.cfgbuilder']
}

compileJava.dependsOn antlr4

sourceSets.main.java.srcDirs += 'src/generated/java'

configurations {
    compile.extendsFrom antlr4
}

repositories {
    mavenCentral()
}

def junitPlatformVersion = '1.3.1'
def junitJupiterVersion = '5.3.1'
def junit5 = ["org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}",
              "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}",
              "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"]

def slf4j = 'org.slf4j:slf4j-api:1.7.25'
def slf4jBinding = ['ch.qos.logback:logback-core:1.2.3', 'ch.qos.logback:logback-classic:1.2.3']

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.google.guava:guava:26.0-jre"
    compile "guru.nidi:graphviz-java:0.7.0"
    compile slf4j, slf4jBinding
    testCompile junit5
    testCompile "org.assertj:assertj-core:3.11.1"
}

task fatJar(type: Jar) {
//    manifest {
//        attributes 'Implementation-Title': 'Python-Parser',
//                'Implementation-Version': '0.0.1'
//    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}